

#version 460

#extension GL_EXT_ray_tracing : enable
#extension GL_EXT_nonuniform_qualifier : enable

layout (set = 0, binding = 0) uniform writeonly image2D imageOutput;


layout (set = 0, binding = 3, r8ui) uniform readonly uimage3D voxelData[512];
layout (set = 0, binding = 4, r16ui) uniform readonly uimage3D voxelChunkMap;



layout (set = 0, binding = 1) uniform Transform
{
    mat4 view;
    mat4 invproj;
} transform;

//=================
//  Materials
//-----------------
#define MAT_AIR 0
#define MAT_STONE 1
#define MAT_STONE2 2
#define MAT_STONE3 3
#define MAT_GRASS 9
#define MAT_FLOWER 10
#define MAT_WATER 19
// ================

bool RayIntersectsCube(vec3 corner, float size, vec3 rayOrigin, vec3 rayDir, out float tMin, out float tMax) {
    // The cube is defined from corner (0, 0, 0) to corner (size, size, size)
    vec3 minBound = corner;                  // Minimum corner at (0,0,0)
    vec3 maxBound = corner + vec3(size);     // Maximum corner at (size, size, size)

    vec3 invDir = 1.0 / rayDir;
    vec3 t1 = (minBound - rayOrigin) * invDir;
    vec3 t2 = (maxBound - rayOrigin) * invDir;

    vec3 tMinVec = min(t1, t2);
    vec3 tMaxVec = max(t1, t2);

    tMin = max(max(tMinVec.x, tMinVec.y), tMinVec.z);
    tMax = min(min(tMaxVec.x, tMaxVec.y), tMaxVec.z);

    return tMax >= tMin && tMax >= 0.0;
}

bool DistanceToCubeSurface(vec3 pointInside, vec3 direction, vec3 corner, float size, out float distance) {
    // Define the minimum and maximum bounds of the cube
    direction = normalize(direction);
    vec3 minBound = corner;                  // Minimum corner at (0, 0, 0)
    vec3 maxBound = corner + vec3(size);     // Maximum corner at (size, size, size)
    
   // Calculate t values for each axis (distance to each face in the direction)
    vec3 t1 = (minBound - pointInside) / direction;
    vec3 t2 = (maxBound - pointInside) / direction;
    
    // Swap t1 and t2 if direction is negative (so t1 refers to the closer intersection)
    vec3 tMinVec = min(t1, t2);  // Smallest values for entry to the cube
    vec3 tMaxVec = max(t1, t2);  // Largest values for exit from the cube
    
    // The distance is the smallest positive tMax where the direction is outward
    distance = max(max(tMinVec.x, tMinVec.y), tMinVec.z);  // Use the smallest positive tMax for the hit point
    
    
    // Check if the point is actually inside the cube and the direction is outward
    return (distance >= 0.0); 
}

vec4 computer_ray_dir(uvec3 launchID, uvec3 launchSize)
{
    float x = (2 * float(launchID.x) + 0.5) / float(launchSize.x) - 1.0;
    float y = (1.0 - 2 * (float(launchID.y) + 0.5) / float(launchSize.y));

    vec4 direction = transform.invproj * vec4(x,y,1,1);
    direction = normalize(direction);

    return direction;
}

uint GetVoxel(ivec3 position)
{
    ivec3 chunkPosition = ivec3(vec3(float(position.x / 128.0), float(position.y / 128.0), float(position.z / 128.0)));
    uint value = uint(imageLoad(voxelChunkMap, ivec3(chunkPosition)));

    return uint(imageLoad(voxelData[nonuniformEXT(value)], ivec3(mod(position.x, 128), mod(position.y, 128), mod(position.z, 128))));
}

float semiRandomFloat(ivec3 inp) {
    // A simple hashing function using the input vector components
    uint hash = inp.x * 123456789 + inp.y * 987654321 + inp.z * 567890123;
    
    // A bitwise operation to mix the hash value
    hash = (hash ^ (hash >> 21)) * 2654435761u;
    hash = hash ^ (hash >> 21);
    hash = hash * 668265263;
    hash = hash ^ (hash >> 21);

    // Return a float between 0 and 1 based on the hash
    return float(hash & 0xFFFFFFF) / float(0xFFFFFFF);
}

const vec3 FOG_COLOR = vec3(5.0/256.0, 5.0/256.0, 5.0/256);
const vec3 FLOWER_COLRS[] = {vec3(0.8, 0.2, 0.2), vec3(0.0, 0.4, 0.2), vec3(0.5, 0.5, 0.1), vec3(0.95, 0.95, 0.95)};

vec4 getColor(uint material, float d, vec3 hit)
{
    vec3 c = vec3(0.0);
    switch (material)
    {
        case MAT_STONE:
            c = vec3(0.15) * (1.0 - (semiRandomFloat(ivec3(hit.z + (uint(semiRandomFloat(ivec3(hit/2))*2)), 0, 0)) - 0.5) * 0.15);
            break;
        case MAT_STONE2:
            c = vec3(0.15) * (1.0 - (semiRandomFloat(ivec3(hit.z+(uint(semiRandomFloat(ivec3(hit/3))*2)), 0, 0)) - 0.5) * 0.15);
            break;
        case MAT_STONE3:
            c = vec3(0.15) * (1.0 - (semiRandomFloat(ivec3(hit.z+(uint(semiRandomFloat(ivec3(hit/4))*2)), 0, 0)) - 0.5) * 0.15);
            break;
        case MAT_GRASS:
            c = vec3(0.2, 0.5, 0) * (1.0 - (semiRandomFloat(ivec3(hit.z, 0, 0)) - 0.5) * 0.2);
            break;
        case MAT_FLOWER:
            c = FLOWER_COLRS[int(floor(min(semiRandomFloat(ivec3(hit.z, 0, 0)) * 4, 3.5)))];
            break;
        case MAT_WATER:
            c = vec3(0.0, 0.1, 0.5);
            break;
        default:
            break;
    }
    if (material != MAT_AIR)
    {
        c = mix(c, FOG_COLOR, clamp((d - 100.0) / 50.0, 0.0, 1.0));
    }
    return vec4(c, 1.0);
}

vec4 march(vec3 rayOrigin, vec3 rayDir)
{
    float tMax = 0, tMin = 0;

    ivec3 origin = ivec3(0);
    int size = 1024;

    if (rayOrigin.x > 0 && rayOrigin.y > 0 && rayOrigin.z > 0)
    {
        origin = ivec3(rayOrigin);
        size = 1;
    }

    uint value = GetVoxel(ivec3(origin));
    uint layer = ((value >> 5) & 7) ;
    uint material = value & 31;

    if (RayIntersectsCube((origin / uint(pow(2, layer))) * uint(pow(2, layer)), uint(pow(2, layer)), rayOrigin, rayDir, tMin, tMax))
    {
        vec3 hit = rayOrigin;
        float distanceToBox = tMin;
        float rayDistance = distanceToBox;

        if (rayOrigin.x > 0 && rayOrigin.y > 0 && rayOrigin.z > 0)
        {
            distanceToBox = abs(tMax);
        }
        hit += rayDir * distanceToBox; //distanceToBox;
        hit += rayDir * 0.005;

        value = GetVoxel(ivec3(hit));
        layer = ((value >> 5) & 7);
        material = value & 31;

        if (material != MAT_AIR) return getColor(material, length(rayOrigin - hit), hit);



        for (int i = 0; i < 500; i++)
        {
            RayIntersectsCube((ivec3(hit) / uint(pow(2, layer))) * uint(pow(2, layer)), uint(pow(2, layer)), rayOrigin, rayDir, tMin, tMax);
            distanceToBox = abs(tMin - tMax);
            hit += rayDir * distanceToBox;
            rayDistance += distanceToBox;

            value = GetVoxel(ivec3(hit));
            layer = ((value >> 5) & 7);
            material = value & 31;

            if (material != MAT_AIR) return getColor(material, length(rayOrigin - hit), hit);

            if (rayDistance > 400) break;
        }
    }
    return vec4(FOG_COLOR, 1.0);
}

void main()
{



    ivec2 pixelCoord = ivec2(gl_LaunchIDEXT.xy);
    // vec2 ndc = (vec2(pixelCoord) / gl_LaunchSizeEXT.xy) * 2.0 - 1.0;
    vec3 rayOrigin = -transform.view[3].xyz;

    vec4 rayWorldSpace = computer_ray_dir(gl_LaunchIDEXT.xyz, gl_LaunchSizeEXT.xyz);
    // rayWorldSpace /= rayWorldSpace.w;
    mat4 copy = transform.view;
    copy[3] = vec4(0.0, 0.0, 0.0, 1.0);
    vec3 rayDir = normalize((copy * rayWorldSpace).xyz);


    vec4 finalColor = march(rayOrigin, rayDir);


    imageStore(imageOutput, pixelCoord, finalColor);
}